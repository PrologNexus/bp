Start -> lower Predicates Next

Next -> 
Next -> Start

Predicates -> Predicate
Predicates -> ( Pred

Pred -> Terms ) Predicate

# Facts and rules
Predicate -> .
Predicate -> :- Body .

# Body of an rule
Body -> lower BodyLower BodyTerm BodyCont
Body -> const = Term BodyCont
Body -> var = Term BodyCont

# Allows using predicate with or without arguments in body
BodyLower -> 
BodyLower -> ( Terms )

# Allows chaining of goals in a rule
BodyCont -> 
BodyCont -> , Body

# Allows unifying two compounds or using one compound as a goal
BodyTerm ->
BodyTerm -> = Term 

Term -> lower TermLower
Term -> const
Term -> [ ListInner ]
Term -> var

ListInner -> 
ListInner -> Terms ListCons

# Allows for splitting array, e.g. [Head | Tail], [H1, H2 | Tail] etc.
ListCons ->
ListCons -> | Term

Terms -> Term TermsCont

TermsCont ->
TermsCont -> , Terms

TermLower -> 
TermLower -> ( Terms )