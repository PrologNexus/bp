Start ->
Start -> lower Predicates Start

Predicates -> Predicate
Predicates -> ( Pred

Pred -> Terms ) Predicate

# Facts and rules
Predicate -> .
Predicate -> :- Body .

# Body of an rule
Body -> lower TermLower BodyTerm BodyCont
Body -> const = Expr3 BodyCont
Body -> var = Expr3 BodyCont
Body -> List = Expr3 BodyCont
Body -> ! BodyCont

# Allows chaining of goals in a rule
BodyCont -> 
BodyCont -> , Body

# Allows unifying two compounds or using one compound as a goal
BodyTerm ->
BodyTerm -> = Expr3 

List -> [ ListInner ]

ListInner -> 
ListInner -> Terms ListCons

# Allows for splitting array, e.g. [Head | Tail], [H1, H2 | Tail] etc.
ListCons ->
ListCons -> | Expr3

Terms -> Expr3 TermsCont

TermsCont ->
TermsCont -> , Terms

TermLower -> 
TermLower -> ( Terms )

# Expressions for the is operator
Expr3 -> Expr2 Expr3R
Expr3R -> < Expr2 Expr3R 
Expr3R -> > Expr2 Expr3R
Expr3R -> <= Expr2 Expr3R
Expr3R -> >= Expr2 Expr3R
Expr3R ->

Expr2 -> Expr1 Expr2R
Expr2R -> + Expr1 Expr2R
Expr2R -> - Expr1 Expr2R
Expr2R ->

Expr1 -> Expr Expr1R
Expr1R -> * Expr Expr1R
Expr1R -> / Expr Expr1R
Expr1R ->

Expr -> lower TermLower
Expr -> const
Expr -> List
Expr -> var
Expr -> ( Expr3 )
