Start ->
Start -> lower Predicates Start

# Facts and rules
Predicate -> .
Predicate -> :- Body .

Predicates -> Predicate
Predicates -> ( Terms ) Predicate

Operator -> =
Operator -> is

# Body of an rule
Body -> Expr2 BodyOperator BodyCont
Body -> ! BodyCont

BodyOperator -> 
BodyOperator -> Operator Expr2

# Allows chaining of goals in a rule
BodyCont -> 
BodyCont -> , Body

List -> [ ListInner ]

ListInner -> 
ListInner -> Terms ListCons

# Allows for splitting array, e.g. [Head | Tail], [H1, H2 | Tail] etc.
ListCons ->
ListCons -> | Expr2

Terms -> Expr2 TermsCont

TermsCont ->
TermsCont -> , Terms

TermLower -> 
TermLower -> ( Terms )

# Expressions for the is operator
Expr2 -> Expr1 Expr2R
Expr2R -> + Expr1 Expr2R
Expr2R -> - Expr1 Expr2R
Expr2R ->

Expr1 -> Expr Expr1R
Expr1R -> * Expr Expr1R
Expr1R -> / Expr Expr1R
Expr1R ->

Expr -> lower TermLower
Expr -> const
Expr -> List
Expr -> var
Expr -> ( Expr2 )
