Start ->
Start -> lower Predicates Start

Predicates -> Predicate
Predicates -> ( Pred

Pred -> Terms ) Predicate

# Facts and rules
Predicate -> .
Predicate -> :- Body .

Operator -> =
Operator -> is

# Body of an rule
Body -> lower TermLower BodyTerm BodyCont
Body -> const Operator Expr2 BodyCont
Body -> var Operator Expr2 BodyCont
Body -> List Operator Expr2 BodyCont
Body -> ! BodyCont

# Allows chaining of goals in a rule
BodyCont -> 
BodyCont -> , Body

# Allows unifying two compounds or using one compound as a goal
BodyTerm ->
BodyTerm -> Operator Expr2 

List -> [ ListInner ]

ListInner -> 
ListInner -> Terms ListCons

# Allows for splitting array, e.g. [Head | Tail], [H1, H2 | Tail] etc.
ListCons ->
ListCons -> | Expr2

Terms -> Expr2 TermsCont

TermsCont ->
TermsCont -> , Terms

TermLower -> 
TermLower -> ( Terms )

# Expressions for the is operator
Expr2 -> Expr1 Expr2R
Expr2R -> + Expr1 Expr2R
Expr2R -> - Expr1 Expr2R
Expr2R ->

Expr1 -> Expr Expr1R
Expr1R -> * Expr Expr1R
Expr1R -> / Expr Expr1R
Expr1R ->

Expr -> lower TermLower
Expr -> const
Expr -> List
Expr -> var
Expr -> ( Expr3 )
